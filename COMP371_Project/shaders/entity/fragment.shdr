#version 330 core

out vec4 o_fragColor;

in vec3 o_fragPosition;
in vec3 o_fragNormal;
in vec2 o_textureCoordinate;

struct Material {
    sampler2D diffuse;
    sampler2D specular;

    float shininess;
};

struct Light {
    vec3 position;
    vec3 ambient;
    vec3 diffuse;
    vec3 specular;
    vec2 planeNearFar;

    float kc;
    float kl;
    float kq;
};

uniform bool u_shadowsEnabled;
uniform bool u_texturesEnabled;
uniform int u_gridSamples;
uniform float u_gridOffset;
uniform float u_gridFactor;
uniform float u_biasMin;
uniform float u_biasMax;
uniform vec3 u_cameraPosition;
uniform vec4 u_color;
uniform samplerCube u_depthTexture;
uniform Light u_light;
uniform Material u_material;

vec3 g_gridDisk[20] = vec3[] (
    vec3(1, 1,  1), vec3( 1, -1,  1), vec3(-1, -1,  1), vec3(-1, 1,  1), 
    vec3(1, 1, -1), vec3( 1, -1, -1), vec3(-1, -1, -1), vec3(-1, 1, -1),
    vec3(1, 1,  0), vec3( 1, -1,  0), vec3(-1, -1,  0), vec3(-1, 1,  0),
    vec3(1, 0,  1), vec3(-1,  0,  1), vec3( 1,  0, -1), vec3(-1, 0, -1),
    vec3(0, 1,  1), vec3( 0, -1,  1), vec3( 0, -1, -1), vec3( 0, 1, -1)
);

vec3 lightingAmbient() {
    // ambient lighting
    vec3 ambient = u_light.ambient
        * (u_texturesEnabled
            ? texture(u_material.diffuse, o_textureCoordinate).rgb
            : u_color.rgb);

    return ambient;
}

vec3 lightingDiffuse() {
    // diffuse lighting
    vec3 fragNormal = normalize(o_fragNormal);
    vec3 lightDirection = normalize(u_light.position - o_fragPosition);

    float diffusion = max(dot(fragNormal, lightDirection), 0.0f);
    vec3 diffuse = u_light.diffuse
        * diffusion
        * (u_texturesEnabled
            ? texture(u_material.diffuse, o_textureCoordinate).rgb
            : u_color.rgb);

    return diffuse;
}

vec3 lightingSpecular() {
    // specular lighting
    vec3 fragNormal = normalize(o_fragNormal);
    vec3 lightDirection = normalize(u_light.position - o_fragPosition);
    vec3 cameraDirection = normalize(u_cameraPosition - o_fragPosition);
    vec3 reflectionDirection = reflect(-lightDirection, fragNormal);

    float specularity = pow(max(dot(cameraDirection, reflectionDirection), 0.0f), u_material.shininess);
    vec3 specular = u_light.specular
        * specularity
        * (u_texturesEnabled
            ? texture(u_material.specular, o_textureCoordinate).rgb
            : u_color.rgb);

    return specular;
}

float attenuationFactor() {
    // light attenuation over distance
    float distance = length(u_light.position - o_fragPosition);
    float attenuation = 1.0f / (u_light.kc + u_light.kl * distance + u_light.kq * distance * distance);

    return attenuation;
}

float shadowFactor() {
    // shadow mapping calculations
    vec3 lightDirection = normalize(u_light.position - o_fragPosition);

    // vector between light and fragment
    vec3 fragToLight = o_fragPosition - u_light.position;
    float rayLength = length(fragToLight);

    // distance between camera and fragment
    float cameraToFrag = length(u_cameraPosition - o_fragPosition);

    // take samples around fragment to produce softer shadows (PCF)
    float shadow = 0.0f;
    float gridRadius = (u_gridOffset + (cameraToFrag / u_light.planeNearFar.y)) / u_gridFactor;
    for (int i = 0; i != u_gridSamples; ++i) {
        float minimumDepth = texture(u_depthTexture, fragToLight + g_gridDisk[i] * gridRadius).r;

        // bring depth value back to (0, u_light.planeNearFar.y) range
        minimumDepth *= u_light.planeNearFar.y;

        // add offset to limit z-fighting
        float bias = max(u_biasMax * (1.0f - dot(o_fragNormal, lightDirection)), u_biasMin);

        if ((minimumDepth + bias) < rayLength)
            shadow += 1.0f;
    }

    // average samples
    shadow /= u_gridSamples;

    return shadow;
}

void main() {
    // calculate combined lighting
    vec3 fragColor;    
    if (u_shadowsEnabled)
        fragColor = (lightingAmbient() + (1.0f - shadowFactor()) * (lightingDiffuse() + lightingSpecular()));
    else
        fragColor = lightingAmbient() + lightingDiffuse() + lightingSpecular();
    fragColor *= attenuationFactor();

    o_fragColor = vec4(fragColor, 1.0f);
}